// 這2個Map名稱要一致
%start BatteryCoolingMap::Idle
// 狀態機名稱
%class BatteryCooling_ES80
// 狀態機標頭黨
%header BatteryCooling_ES80.h

// 這2個Map名稱要一致
%map BatteryCoolingMap
%%
// State    Transition              Next State              Action(s)
Idle
{
            Switch_2DrivingMode     DrivingMode             {ModeOperation(DRIVING);}
            Switch_2ChargingMode    ChargingMode            {ModeOperation(CHARGING);}
            Back_2Idle              nil                     {ModeOperation(IDLE);}
}

DrivingMode
{
            Back_2Idle              Idle                    {ModeOperation(IDLE);}
            Switch_2DrivingMode     nil                     {ModeOperation(DRIVING);}
            Switch_2ChargingMode    ChargingMode            {ModeOperation(CHARGING);}
            Run                     RunningDrivingCooling   {CoolingOperation(DRIVING);}
}

ChargingMode
{
            Back_2Idle              Idle                    {ModeOperation(IDLE);}
            Switch_2ChargingMode    nil                     {ModeOperation(CHARGING);}
            Switch_2DrivingMode     DrivingMode             {ModeOperation(DRIVING);}
            Run                     RunningChargingCooling  {CoolingOperation(CHARGING);}
}

RunningDrivingCooling
{
            Run                     nil                     {CoolingOperation(DRIVING);}
            Stop                    DrivingMode             {ModeOperation(DRIVING);}
            Back_2Idle              Idle                    {ModeOperation(IDLE);}
            Switch_2ChargingMode    ChargingMode            {ModeOperation(CHARGING);}
}

RunningChargingCooling
{
            Run                     nil                     {CoolingOperation(CHARGING);}
            Stop                    ChargingMode            {ModeOperation(CHARGING);}
            Back_2Idle              Idle                    {ModeOperation(IDLE);}
            Switch_2DrivingMode     DrivingMode             {ModeOperation(DRIVING);}
}

%%
